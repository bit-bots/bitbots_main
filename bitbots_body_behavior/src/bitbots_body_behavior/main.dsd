#GoAndKickBallMapGoal
$AvoidBall
    NO --> $BallClose + distance:%behavior/body/ball_reaproach_dist
        YES --> $BallKickArea
            NEAR --> @ChangeAction + action:kicking + r:false, @Stop + r:false, @LookAtBall + r:false, @StandAndWait + duration:1 + r:false, @LookForward + r:false, @KickBallDynamic, @LookAtFieldFeatures + r:false, @StandAndWait + duration:1 + r:false
            FAR --> @ChangeAction + action:going_to_ball, @LookAtFieldFeatures, @GoToBall + target:map_goal
        NO --> @ChangeAction + action:going_to_ball + r:false, @LookAtFieldFeatures + r:false, @AvoidBallActive + r:false, @GoToBall + target:map_goal + blocking:false + distance:%behavior/body/ball_far_approach_dist
    YES --> $ReachedMovebaseGoalPosition + thres:%behavior/body/ball_far_approach_position_thresh
        YES --> @AvoidBallInactive
        NO --> @ChangeAction + action:going_to_ball, @LookAtFieldFeatures, @GoToBall + target:map_goal + distance:%behavior/body/ball_far_approach_dist

#GoAndKickBallDetectionGoal
$AlignedToMoveBaseGoal
    YES --> $BallKickArea
        NEAR --> @ChangeAction + action:kicking, @Stop, @LookForward, @KickBallDynamic
        FAR --> @ChangeAction + action:going_to_ball, @GoToBall + target:detection_goal
    NO --> @ChangeAction + action:going_to_ball, @GoToBall + target:detection_goal

#GoAndKickBallAway
$BallKickArea
    NEAR --> @ChangeAction + action:kicking, @Stop, @LookForward, @KickBallDynamic
    FAR --> @ChangeAction + action:going_to_ball, @GoToBall + target:close

#SearchBall
@ChangeAction + action:searching, @LookAtFieldFeatures, @StandAndWait + duration:4, @TurnAround

#NoLocalizationPlayerBehavior
$BallSeen
    YES --> $BallClose
        YES --> $GoalSeen
            YES --> #GoAndKickBallDetectionGoal
            NO --> #GoAndKickBallAway
        NO --> @ChangeAction + action:going_to_ball, @GoToBall + target:close
    NO --> #SearchBall

#NoLocalizationGoalieBehavior
$BallSeen
    YES --> $BallClose
        YES --> #GoAndKickBallAway
        NO --> @ChangeAction + action:waiting, @Stop, @LookAtBall
    NO --> #SearchBall

#PositioningReady
$LocalizationAvailable
    YES --> @ChangeAction + action:positioning, @LookAtFieldFeatures, @StandAndWait + duration:%behavior/body/ready_wait_time, @AvoidBallActive, @GoToRolePosition
    NO --> @ChangeAction + action:positioning, @AvoidBallActive, @GoToRelativePosition + x:2 + y:0 + t:0, @StandAndWait

#ReorientationPause
$TimerRunning + name:reorientation
    YES --> $TimerEnded + name:reorientation
        YES --> @StartTimer + name:reorientation_pause + duration:%behavior/body/reorientation_pause_duration
        NO --> @ChangeAction + action:localizing, @LookAtFieldFeatures, @Stop
    NO --> @StartTimer + name:reorientation + duration:%behavior/body/reorientation_duration

#GoalieBehavior
$LocalizationAvailable
    YES --> $LocalizationPrecision
        HIGH --> $TimerRunning + name:reorientation
            YES --> @EndTimer + name:reorientation, @StartTimer + name:reorientation_pause + duration:%behavior/body/reorientation_pause_duration
            NO --> $BallSeen
                YES --> $ClosestToBall
                    YES --> $BallDangerous
                        NO --> @ChangeAction + action:positioning, @GoToBlockPosition, @LookAtFieldFeatures
                        ELSE --> #GoAndKickBallMapGoal
                    NO --> @ChangeAction + action:positioning, @AvoidBallActive, @GoToBlockPosition, @LookAtFieldFeatures
                NO --> @ChangeAction + action:searching, @LookAtFieldFeatures
        LOW --> $TimerEnded + name:reorientation_pause
            NO --> #NoLocalizationGoalieBehavior
            YES --> #ReorientationPause
    NO --> #NoLocalizationGoalieBehavior

#OffenseBehavior
$LocalizationAvailable
    YES --> $LocalizationPrecision
        HIGH --> $TimerRunning + name:reorientation
            YES --> @EndTimer + name:reorientation, @StartTimer + name:reorientation_pause + duration:%behavior/body/reorientation_pause_duration
            NO --> $BallSeen
                YES --> $ClosestToBallNoGoalie
                    YES --> $GoalieHandlingBall
                        YES --> @ChangeAction + action:positioning, @AvoidBallActive, @GoToPassAcceptPosition //don't go to far back
                        NO --> #GoAndKickBallMapGoal
                    NO --> $PassStarted
                        YES --> @ChangeAction + action:positioning, @AvoidBallActive, @GoToPassAcceptPosition
                        NO --> @ChangeAction + action:positioning, @AvoidBallActive, @GoToPassPreparePosition
                NO --> #SearchBall
        LOW --> $TimerEnded + name:reorientation_pause
            NO --> #NoLocalizationPlayerBehavior
            YES --> #ReorientationPause
    NO --> #NoLocalizationPlayerBehavior

#DefenseBehavior
$LocalizationAvailable
    YES --> $LocalizationPrecision
        HIGH --> $TimerRunning + name:reorientation
            YES --> @EndTimer + name:reorientation, @StartTimer + name:reorientation_pause + duration:%behavior/body/reorientation_pause_duration
            NO --> $BallSeen
                YES --> $BallInDefensiveArea
                    YES --> $ClosestToBallNoGoalie
                        YES --> $GoalieHandlingBall
                            YES --> @ChangeAction + action:positioning, @GoToDefensePosition
                            NO --> #GoAndKickBallMapGoal
                        NO --> @ChangeAction + action:positioning, @GoToDefensePosition
                    NO -->  $BallClose
                        YES --> $ClosestToBallNoGoalie
                            YES --> #GoAndKickBallMapGoal
                            NO --> @ChangeAction + action:positioning, @GoToDefensePosition
                        NO --> @ChangeAction + action:positioning, @GoToDefensePosition
                NO --> @Stop, @LookAtFieldFeatures
        LOW --> $TimerEnded + name:reorientation_pause
            NO --> #NoLocalizationPlayerBehavior
            YES --> #ReorientationPause
    NO --> #NoLocalizationPlayerBehavior

#PenaltyBehavior
$SecondaryStateTeamDecider
    OUR --> $GameStateDecider //in penalty we use the main game states to start the positioning and the kicks
        READY --> @LookAtBall, @GoToBall + target:map_goal + distance:%behavior/body/ball_far_approach_dist
        PLAYING --> #GoAndKickBallMapGoal
        ELSE --> @LookAtFieldFeatures, @StandAndWait
    OTHER --> $BallDangerous
        LEFT --> @Stop, @LookAtBall, @PlayAnimationGoalieFallLeft
        RIGHT --> @Stop, @LookAtBall, @PlayAnimationGoalieFallRight
        ELSE --> @Stop, @LookAtBall

#GoalieRole
$SecondaryStateDecider
    NORMAL --> $GameStateDecider
        READY --> $GoalScoreRecently
            YES -> @Stop, @PlayAnimationCheering
            NO -> #PositioningReady
        PLAYING --> #GoalieBehavior
        ELSE --> @ChangeAction + action:waiting, @LookAtFieldFeatures, @StandAndWait
    PENALTYSHOOT --> #PenaltyBehavior
    TIMEOUT --> @LookAtFieldFeatures, @StandAndWait
    OVERTIME --> $GameStateDecider
        READY --> #PositioningReady
        PLAYING --> #GoalieBehavior
        ELSE --> @ChangeAction + action:waiting, @LookAtFieldFeatures, @StandAndWait
    ELSE --> $SecondaryStateModeDecider
        PLACING --> #PositioningReady
        ELSE --> @ChangeAction + action:waiting, @LookAtFieldFeatures, @StandAndWait

#OffenseSecondaryState
$SecondaryStateTeamDecider
    OUR --> $BallSeen
        YES --> $ClosestToBallNoGoalie
            YES --> $SecondaryStateModeDecider
                PLACING --> @ChangeAction + action:positioning, @LookAtFieldFeatures, @AvoidBallActive, @GoToBall + target:map_goal
                ELSE --> @ChangeAction + action:waiting, @LookAtFieldFeatures, @StandAndWait
            NO --> $SecondaryStateModeDecider
                PLACING --> @ChangeAction + action:positioning, @LookAtFieldFeatures, @AvoidBallActive, @GoToRolePosition
                ELSE --> @ChangeAction + action:waiting, @LookAtFieldFeatures, @StandAndWait
        NO --> #SearchBall
    OTHER --> $SecondaryStateModeDecider
        PLACING --> $BallSeen
            YES --> @ChangeAction + action:positioning, @LookAtFieldFeatures, @AvoidBallActive, @GoToBall + target:map_goal + distance:2
            NO --> #SearchBall
        ELSE --> @ChangeAction + action:waiting, @LookAtFieldFeatures, @StandAndWait

#OffenseRole
$SecondaryStateDecider
    NORMAL --> $GameStateDecider
        READY --> #PositioningReady
        PLAYING --> #OffenseBehavior
        ELSE --> @ChangeAction + action:waiting, @LookAtFieldFeatures, @StandAndWait
    PENALTYSHOOT --> #PenaltyBehavior
    OVERTIME --> $GameStateDecider
        READY --> #PositioningReady
        PLAYING --> #OffenseBehavior
        ELSE --> @ChangeAction + action:waiting, @LookAtFieldFeatures, @StandAndWait
    TIMEOUT -->  @ChangeAction + action:waiting, @LookAtFieldFeatures, @StandAndWait
    ELSE --> #OffenseSecondaryState


#DefenseRole
$SecondaryStateDecider
    NORMAL --> $GameStateDecider
        READY --> #PositioningReady
        PLAYING --> #DefenseBehavior
        ELSE --> @ChangeAction + action:waiting, @LookAtFieldFeatures, @StandAndWait
    PENALTYSHOOT --> #PenaltyBehavior
    OVERTIME --> $GameStateDecider
        READY --> #PositioningReady
        PLAYING --> #DefenseBehavior
        ELSE --> @ChangeAction + action:waiting, @LookAtFieldFeatures, @StandAndWait
    TIMEOUT --> @ChangeAction + action:waiting, @LookAtFieldFeatures, @StandAndWait
    ELSE --> $SecondaryStateModeDecider
        PLACING --> @AvoidBallActive, @GoToDefensePosition
        ELSE --> @ChangeAction + action:waiting, @LookAtFieldFeatures, @StandAndWait

-->BodyBehavior
$AllowedToMove
    NO_MOVEMENT --> @ChangeAction + action:waiting, @LookForward, @StandAndWait
    ONLY_HEAD --> @ChangeAction + action:waiting, @LookAtFieldFeatures, @StandAndWait
    JUST_UNPENALIZED --> @ChangeAction + action:waiting, @GetWalkready, @StandAndWait + duration:2 + r:false
    NORMAL --> $RoleDecider
        GOALIE --> #GoalieRole
        OFFENSE --> #OffenseRole
        DEFENSE --> #DefenseRole
