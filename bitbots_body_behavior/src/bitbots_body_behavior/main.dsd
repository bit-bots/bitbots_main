#GoAndKickBallMapGoal
$AvoidBall
    NO --> $BallClose + distance:%behavior/body/ball_reaproach_dist
        YES --> $BallKickArea
            NEAR --> @Stop + r:false, @LookAtBall + r:false, @StandAndWait + duration:1 + r:false, @LookForward + r:false, @KickBallDynamic
            FAR --> @LookAtFieldFeatures, @GoToBall + target:gradient_goal
        NO --> @LookAtFieldFeatures, @GoToBall + target:gradient_goal + blocking:false + distance:%behavior/body/ball_far_approach_dist, @AvoidBallActive
    YES --> $ReachedMovebaseGoalPosition + thres:%behavior/body/ball_far_approach_position_thresh
        YES --> @AvoidBallInactive
        NO --> @LookAtFieldFeatures, @GoToBall + target:gradient_goal + distance:%behavior/body/ball_far_approach_dist

#GoAndKickBallDetectionGoal
$AlignedToMoveBaseGoal
    YES --> $BallKickArea
        NEAR --> @Stop, @LookForward, @KickBallDynamic
        FAR --> @GoToBall + target:detection_goal
    NO --> @GoToBall + target:detection_goal

#GoAndKickBallAway
$BallKickArea
    NEAR --> @Stop, @LookForward, @KickBallDynamic
    FAR --> @GoToBall + target:close

#NoLocalizationPlayerBehavior
$BallSeen
    YES --> $BallClose
        YES --> $GoalSeen
            YES --> #GoAndKickBallDetectionGoal
            NO --> #GoAndKickBallAway
        NO --> @GoToBall + target:close
    NO --> @Stop, @LookAtBall, @SearchBall

#NoLocalizationGoalieBehavior
$BallSeen
    YES --> $BallClose
        YES --> #GoAndKickBallAway
        NO --> @Stop, @LookAtBall
    NO --> @Stop, @LookAtBall

#Positioning
$LocalizationAvailable
    YES --> @LookAtFieldFeatures, @StandAndWait + duration:%behavior/body/ready_wait_time, @AvoidBallActive, @GoToRolePosition
    NO --> @GoToRelativePosition + x:2 + y:0 + t:0

#PositioningReady
$LocalizationAvailable
    YES --> @LookAtFieldFeatures, @StandAndWait + duration:%behavior/body/ready_wait_time, @AvoidBallActive, @GoToRolePosition
    NO --> @GoToRelativePosition + x:2 + y:0 + t:0, @StandAndWait

#GoalieBehavior
$LocalizationAvailable
    YES --> $LocalizationPrecision
        HIGH --> $TimerRunning + name:reorientation
            YES --> @EndTimer + name:reorientation, @StartTimer + name:reorientation_pause + duration:%behavior/body/reorientation_pause_duration
            NO --> $JustUnpenalized
                YES --> #Positioning
                NO --> $BallSeen
                    YES --> $BallDangerous
                        NO --> @GoToBlockPosition, @LookAtFieldFeatures
                        ELSE --> #GoAndKickBallMapGoal
                    NO --> @LookAtFieldFeatures
        LOW --> $TimerEnded + name:reorientation_pause
            NO --> #NoLocalizationGoalieBehavior
            YES --> $TimerRunning + name:reorientation
                YES --> $TimerEnded + name:reorientation
                    YES --> @StartTimer + name:reorientation_pause + duration:%behavior/body/reorientation_pause_duration
                    NO --> @LookAtFieldFeatures, @Stop
                NO --> @StartTimer + name:reorientation + duration:%behavior/body/reorientation_duration
    NO --> $JustUnpenalized
        YES --> @GoToRelativePosition + x:2 + y:0 + t:0
        NO --> #NoLocalizationGoalieBehavior

#NoLocalizationOffenseBehavior
$JustUnpenalized
    YES --> @GoToRelativePosition + x:2 + y:0 + t:0
    NO --> #NoLocalizationPlayerBehavior


#OffenseBehavior
$LocalizationAvailable
    YES --> $LocalizationPrecision
        HIGH --> $TimerRunning + name:reorientation
            YES --> @EndTimer + name:reorientation, @StartTimer + name:reorientation_pause + duration:%behavior/body/reorientation_pause_duration
            NO --> $JustUnpenalized
                YES --> #Positioning
                NO --> $BallSeen
                    YES --> #GoAndKickBallMapGoal
                    NO --> @Stop, @LookAtFieldFeatures, @SearchBall
        LOW --> $TimerEnded + name:reorientation_pause
            NO --> #NoLocalizationOffenseBehavior
            YES --> $TimerRunning + name:reorientation
                YES --> $TimerEnded + name:reorientation
                    YES --> @StartTimer + name:reorientation_pause + duration:%behavior/body/reorientation_pause_duration
                    NO --> @LookAtFieldFeatures, @Stop
                NO --> @StartTimer + name:reorientation + duration:%behavior/body/reorientation_duration
    NO --> #NoLocalizationOffenseBehavior

#DefenseBehavior
$LocalizationAvailable
    YES --> $LocalizationPrecision
        HIGH --> $TimerRunning + name:reorientation
            YES --> @EndTimer + name:reorientation, @StartTimer + name:reorientation_pause + duration:%behavior/body/reorientation_pause_duration
            NO --> $JustUnpenalized
                YES --> #Positioning
                NO --> $BallSeen
                    YES --> $BallInDefensiveArea
                        YES --> #GoAndKickBallMapGoal
                        NO -->  $BallClose
                            YES --> #GoAndKickBallMapGoal
                            NO --> @GoToDefensePosition
                    NO --> @Stop, @LookAtFieldFeatures
        LOW --> $TimerEnded + name:reorientation_pause
            NO --> #NoLocalizationPlayerBehavior
            YES --> $TimerRunning + name:reorientation
                YES --> $TimerEnded + name:reorientation
                    YES --> @StartTimer + name:reorientation_pause + duration:%behavior/body/reorientation_pause_duration
                    NO --> @LookAtFieldFeatures, @Stop
                NO --> @StartTimer + name:reorientation + duration:%behavior/body/reorientation_duration
    NO --> #NoLocalizationPlayerBehavior

#PenaltyBehavior
$SecondaryStateTeamDecider
    OUR --> $GameStateDecider //in penalty we use the main game states to start the positioning and the kicks
        READY --> @LookAtBall, @GoToBall + target:gradient_goal + distance:%behavior/body/ball_far_approach_dist
        PLAYING --> #GoAndKickBallMapGoal
        ELSE --> @LookAtFieldFeatures, @StandAndWait
    OTHER --> $BallDangerous
        LEFT --> @Stop, @LookAtBall, @PlayAnimationGoalieFallLeft
        RIGHT --> @Stop, @LookAtBall, @PlayAnimationGoalieFallRight
        ELSE --> @Stop, @LookAtBall

#GoalieRole
$SecondaryStateDecider
    PENALTYSHOOT --> #PenaltyBehavior
    TIMEOUT --> @LookAtFieldFeatures, @StandAndWait
    OVERTIME --> $GameStateDecider
        INITIAL --> @LookAtFieldFeatures, @StandAndWait
        READY --> #PositioningReady
        PLAYING --> #GoalieBehavior
        ELSE --> @LookAtFieldFeatures, @StandAndWait
    ELSE --> $SecondaryStateModeDecider
        PLACING --> #PositioningReady
        ELSE --> @LookAtFieldFeatures, @StandAndWait

#OffenseSecondaryState
$SecondaryStateTeamDecider
    OUR --> $BallSeen
        YES --> $ClosestToBallNoGoalie
            YES --> $SecondaryStateModeDecider
                PLACING --> @LookAtFieldFeatures, @GoToBall + target:gradient_goal
                ELSE --> @LookAtFieldFeatures, @StandAndWait
            NO --> $SecondaryStateModeDecider
                PLACING --> @LookAtFieldFeatures, @GoToRolePosition
                ELSE --> @LookAtFieldFeatures, @StandAndWait
        NO --> @LookAtFieldFeatures, @SearchBall
    OTHER --> $SecondaryStateModeDecider
        PLACING --> $BallSeen
            YES --> @LookAtFieldFeatures, @GoToBall + target:map_goal + distance:2
            NO --> @LookAtFieldFeatures, @SearchBall
        ELSE --> @LookAtFieldFeatures, @StandAndWait

#OffenseRole
$SecondaryStateDecider
    NORMAL --> $GameStateDecider
        INITIAL --> @LookAtFieldFeatures, @StandAndWait
        READY --> #PositioningReady
        PLAYING --> #OffenseBehavior
        ELSE --> @LookAtFieldFeatures, @StandAndWait
    PENALTYSHOOT --> #PenaltyBehavior
    OVERTIME --> $GameStateDecider
        INITIAL --> @LookAtFieldFeatures, @StandAndWait
        READY --> #PositioningReady
        PLAYING --> #OffenseBehavior
        ELSE --> @LookAtFieldFeatures, @StandAndWait
    TIMEOUT -->  @LookAtFieldFeatures, @StandAndWait
    ELSE --> #OffenseSecondaryState


#DefenseRole
$SecondaryStateDecider
    NORMAL --> $GameStateDecider
        INITIAL --> @LookAtFieldFeatures, @StandAndWait
        READY --> #PositioningReady
        PLAYING --> #DefenseBehavior
        ELSE --> @LookAtFieldFeatures, @StandAndWait
    PENALTYSHOOT --> #PenaltyBehavior
    OVERTIME --> $GameStateDecider
        INITIAL --> @LookAtFieldFeatures, @StandAndWait
        READY --> #PositioningReady
        PLAYING --> #DefenseBehavior
        ELSE --> @LookAtFieldFeatures, @StandAndWait
    TIMEOUT --> @LookAtFieldFeatures, @StandAndWait
    ELSE --> $SecondaryStateModeDecider
        PLACING --> @GoToDefensePosition
        ELSE --> @LookAtFieldFeatures, @StandAndWait

#DefenseRole
$SecondaryStateDecider
    NORMAL --> $GameStateDecider
        INITIAL --> @LookAtFieldFeatures, @StandAndWait
        READY --> #PositioningReady
        PLAYING --> #DefenseBehavior
        ELSE --> @LookAtFieldFeatures, @StandAndWait
    PENALTYSHOOT --> #PenaltyBehavior
    OVERTIME --> $GameStateDecider
        INITIAL --> @LookAtFieldFeatures, @StandAndWait
        READY --> #PositioningReady
        PLAYING --> #DefenseBehavior
        ELSE --> @LookAtFieldFeatures, @StandAndWait
    TIMEOUT --> @LookAtFieldFeatures, @StandAndWait
    ELSE --> $SecondaryStateModeDecider
        PLACING --> @GoToDefensePosition
        ELSE --> @LookAtFieldFeatures, @StandAndWait

-->BodyBehavior
$RoleDecider
    GOALIE --> #GoalieRole
    OFFENSE --> #OffenseRole
    DEFENSE --> #DefenseRole
    PENALTY --> @LookAtFieldFeatures, @StandAndWait
