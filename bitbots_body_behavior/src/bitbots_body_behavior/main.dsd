#SearchBall
@ChangeAction + action:searching, @LookAtFieldFeatures, @WalkInPlace + duration:4, @TurnAround

#DoNothing
@ChangeAction + action:waiting, @LookForward, @Stand

#StandAndLook
@ChangeAction + action:waiting, @LookAtFieldFeatures, @Stand

#GetWalkreadyAndLocalize
@ChangeAction + action:waiting + r:false, @PlayAnimationInit + r:false, @LookAtFieldFeatures + r:false, @GetWalkready + r:false, @WalkInPlace


#GoAndKickBallMapGoal
$DribbleOrKick
    DRIBBLE --> @CancelPathplanning, @LookAtFront, @DribbleForward
    KICK --> $AvoidBall
        NO --> $BallClose + distance:%behavior/body/ball_reaproach_dist + angle:%behavior/body/ball_reapproach_angle
            YES --> $BallKickArea
                NEAR --> @ChangeAction + action:kicking, @LookAtFront, @Stand + duration:1.0, @LookForward + r:false, @KickBallDynamic, @LookAtFieldFeatures + r:false, @WalkInPlace + duration:1 + r:false
                FAR --> @ChangeAction + action:going_to_ball, @LookAtFront, @GoToBall + target:map_goal
            NO --> @ChangeAction + action:going_to_ball + r:false, @LookAtFieldFeatures + r:false, @AvoidBallActive + r:false, @GoToBall + target:map_goal + blocking:false + distance:%behavior/body/ball_far_approach_dist
        YES --> $ReachedMovebaseGoalPosition + thres:%behavior/body/ball_far_approach_position_thresh
            YES --> @AvoidBallInactive
            NO --> @ChangeAction + action:going_to_ball, @LookAtFieldFeatures, @GoToBall + target:map_goal + distance:%behavior/body/ball_far_approach_dist

#GoAndKickBallDetectionGoal
$AlignedToMoveBaseGoal
    YES --> $BallKickArea
        NEAR --> @ChangeAction + action:kicking + r:false, @Stand + duration:0.1 + r:false, @LookForward + r:false, @KickBallDynamic + r:false
        FAR --> @ChangeAction + action:going_to_ball, @GoToBall + target:detection_goal
    NO --> @ChangeAction + action:going_to_ball, @GoToBall + target:detection_goal

#GoAndKickBallAway
$BallKickArea
    NEAR --> @ChangeAction + action:kicking + r:false, @Stand + duration:0.1 + r:false, @LookForward + r:false, @KickBallDynamic + r:false
    FAR --> @ChangeAction + action:going_to_ball, @GoToBall + target:close

#NoLocalizationPlayerBehavior
$BallSeen
    YES --> $BallClose
        YES --> $GoalSeen
            YES --> #GoAndKickBallDetectionGoal
            NO --> #GoAndKickBallAway
        NO --> @ChangeAction + action:going_to_ball, @GoToBall + target:close
    NO --> #SearchBall

#NoLocalizationGoalieBehavior
$BallSeen
    YES --> $BallClose
        YES --> #GoAndKickBallAway
        NO --> @ChangeAction + action:waiting, @LookAtBall, @Stand
    NO --> #SearchBall

#PositioningReady
$ConfigRole
    GOALIE --> $GoalScoreRecently
        YES --> @Stand + duration:0.1 + r:false, @PlayAnimationCheering + r:false, @GetWalkready + r:false
        NO --> $LocalizationAvailable
            YES --> @ChangeAction + action:positioning, @LookAtFieldFeatures, @Stand + duration:%behavior/body/ready_wait_time, @AvoidBallActive, @GoToRolePosition
            NO --> @ChangeAction + action:positioning, @AvoidBallActive, @GoToRelativePosition + x:2 + y:0 + t:0, @Stand
    ELSE --> $LocalizationAvailable
        YES --> @ChangeAction + action:positioning, @LookAtFieldFeatures, @Stand + duration:%behavior/body/ready_wait_time, @AvoidBallActive, @GoToRolePosition
        NO --> @ChangeAction + action:positioning, @AvoidBallActive, @GoToRelativePosition + x:2 + y:0 + t:0, @Stand

#ReorientationPause
$TimerRunning + name:reorientation
    YES --> $TimerEnded + name:reorientation
        YES --> @StartTimer + name:reorientation_pause + duration:%behavior/body/reorientation_pause_duration
        NO --> @ChangeAction + action:localizing, @LookAtFieldFeatures, @Stand
    NO --> @StartTimer + name:reorientation + duration:%behavior/body/reorientation_duration

#GoalieBehavior
$ClosestToBall + use_time_to_ball:true
    YES --> $BallDangerous
        NO --> @ChangeAction + action:positioning, @AvoidBallActive, @LookAtFieldFeatures, @GoToBlockPosition
        ELSE --> #GoAndKickBallMapGoal
    NO --> @ChangeAction + action:positioning, @LookAtFieldFeatures, @GoToBlockPosition

#StrikerRole
$GoalieHandlingBall
    YES --> @ChangeAction + action:positioning, @AvoidBallActive, @LookAtFieldFeatures, @GoToPassAcceptPosition //don't go too far back
    NO --> #GoAndKickBallMapGoal

#SupporterRole
$PassStarted
    YES --> @LookAtBall, @ChangeAction + action:positioning, @AvoidBallActive, @GoToPassAcceptPosition
    NO --> @LookAtFieldFeatures, @ChangeAction + action:positioning, @AvoidBallActive, @GoToPassPreparePosition

#DefenderRole
@LookAtFieldFeatures, @ChangeAction + action:positioning, @GoToDefensePosition

#GoalieRole
$LocalizationAvailable
    YES --> $LocalizationPrecision
        HIGH --> $TimerRunning + name:reorientation
            YES --> @EndTimer + name:reorientation + r:false, @StartTimer + name:reorientation_pause + duration:%behavior/body/reorientation_pause_duration + r:false
            NO --> $CurrentScore
                BEHIND --> $LastPlayer
                    YES --> #StrikerRole
                    NO --> #GoalieBehavior
                ELSE --> #GoalieBehavior
        LOW --> $TimerEnded + name:reorientation_pause
            NO --> #NoLocalizationGoalieBehavior
            YES --> #ReorientationPause
    NO --> #NoLocalizationGoalieBehavior

#PenaltyShootoutBehavior
$IsPenaltyShootRobot
    NO --> #DoNothing
    YES -->$SecondaryStateTeamDecider
        OUR --> @StandAndWaitRandom + min:10 + max:30, @KickBallDynamic + r:true + type:penalty, @Stand
        OTHER --> $BallDangerous
            LEFT --> @PlayAnimationGoalieFallLeft, @Stand
            RIGHT --> @PlayAnimationGoalieFallRight, @Stand
            CENTER --> @PlayAnimationGoalieFallCenter, @Stand
            ELSE --> @LookAtBall


#Placing
$ConfigRole
    GOALIE --> @ChangeAction + action:positioning, @GoToBlockPosition, @LookAtFieldFeatures
    ELSE --> $BallSeen
        NO --> #SearchBall
        YES --> $SecondaryStateDecider
            CORNER_KICK --> $SecondaryStateTeamDecider
                OUR --> $RankToBallNoGoalie + use_time_to_ball:true
                    FIRST --> @ChangeAction + action:positioning, @LookAtFieldFeatures, @AvoidBallActive, @GoToCornerKickPosition + mode:striker
                    SECOND --> @ChangeAction + action:positioning, @LookAtFieldFeatures, @AvoidBallActive, @GoToCornerKickPosition + mode:supporter
                    THIRD --> @ChangeAction + action:positioning, @AvoidBallActive, @GoToDefensePosition
                OTHER --> @ChangeAction + action:positioning, @LookAtFieldFeatures, @AvoidBallActive, @GoToCornerKickPosition + mode:others
            ELSE --> $SecondaryStateTeamDecider
                OUR --> $RankToBallNoGoalie + use_time_to_ball:true
                    FIRST --> @ChangeAction + action:positioning, @LookAtFieldFeatures, @AvoidBallActive, @GoToBall + target:gradient_goal + distance:0.5
                    SECOND --> @ChangeAction + action:positioning, @LookAtFieldFeatures, @AvoidBallActive, @GoToPassPreparePosition
                    THIRD --> @ChangeAction + action:positioning, @LookAtFieldFeatures, @AvoidBallActive, @GoToDefensePosition
                OTHER --> $RankToBallNoGoalie
                    FIRST --> @ChangeAction + action:positioning, @LookAtFieldFeatures, @AvoidBallActive, @GoToDefensePosition + mode:freekick_first
                    SECOND --> @ChangeAction + action:positioning, @LookAtFieldFeatures, @AvoidBallActive, @GoToDefensePosition + mode:freekick_second
                    THIRD --> @ChangeAction + action:positioning, @LookAtFieldFeatures, @AvoidBallActive, @GoToDefensePosition

#Init
@Stand + duration:0.1 + r:false, @PlayAnimationInit + r:false, @ChangeAction + action:waiting, @LookForward, @Stand

-->BodyBehavior
$IsPenalized
    YES --> #DoNothing
    JUST_UNPENALIZED --> #GetWalkreadyAndLocalize
    NO --> $GameStateDecider
        INITIAL --> #Init
        READY --> $AnyGoalScoreRecently + time:50
            YES --> #PositioningReady
            NO --> $DoOnce
                NOT_DONE --> @ChangeAction + action:waiting + r:false, @LookAtFieldFeatures + r:false, @Stand + duration:1, @GetWalkready + r:false
                DONE --> #PositioningReady
        SET --> $SecondaryStateDecider
            PENALTYSHOOT --> $SecondaryStateTeamDecider
                OUR --> @Stand + duration:0.1 + r:false, @DeactivateHCM + r:false, @LookForward + r:false, @PlayAnimationInit + r:false, @GetWalkready + r:false, @Stand // we need to also see the goalie
                OTHER --> @Stand + duration:0.1 + r:false, @DeactivateHCM + r:false, @LookForward + r:false, @PlayAnimationInit + r:false, @GetWalkready + r:false, @LookAtBall + r:false, @PlayAnimationGoalieArms + r:false, @Stand // goalie only needs to care about the ball
            ELSE --> #StandAndLook
        FINISHED --> #Init
        PLAYING --> $SecondaryStateDecider
            PENALTYSHOOT --> #PenaltyShootoutBehavior
            TIMEOUT --> #StandAndLook
            ELSE --> $SecondaryStateModeDecider
                ELSE --> #StandAndLook
                PLACING --> #Placing
            NORMAL --> $BallSeen
                NO --> $ConfigRole
                    GOALIE --> @GoToRolePosition
                    ELSE --> #SearchBall
                YES --> $KickOffTimeUp
                    NO_NORMAL --> #StandAndLook
                    NO_FREEKICK --> #Placing
                    YES --> $ConfigRole
                        GOALIE --> $BallInOwnPercent + p:33
                            YES --> #GoalieRole
                            NO --> #DefenderRole
                        ELSE --> $LocalizationAvailable
                            YES --> $LocalizationPrecision
                                HIGH --> $TimerRunning + name:reorientation
                                    YES --> @EndTimer + name:reorientation + r:false, @StartTimer + name:reorientation_pause + duration:%behavior/body/reorientation_pause_duration + r:false
                                    NO --> $RankToBallNoGoalie + use_time_to_ball:true
                                        FIRST --> #StrikerRole
                                        SECOND --> #SupporterRole
                                        THIRD --> #DefenderRole
                                LOW --> $TimerEnded + name:reorientation_pause
                                    NO --> #NoLocalizationPlayerBehavior
                                    YES --> #ReorientationPause
                            NO --> #NoLocalizationPlayerBehavior
