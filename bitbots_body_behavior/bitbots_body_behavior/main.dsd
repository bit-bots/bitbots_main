#SearchBall
@ChangeAction + action:searching, @LookAtFieldFeatures, @WalkInPlace + duration:3, @TurnAround

#DoNothing
@ChangeAction + action:waiting, @LookForward, @Stand

#StandAndLook
@ChangeAction + action:waiting, @LookAtFieldFeatures, @Stand

#GetWalkreadyAndLocalize
@ChangeAction + action:waiting + r:false, @PlayAnimationInitInSim + r:false, @LookAtFieldFeatures + r:false, @GetWalkready + r:false, @WalkInPlace

#PerformKick
@ChangeAction + action:kicking, @LookAtFront, @Stand + duration:1.0, @LookForward + r:false, @KickBallDynamic, @LookAtFieldFeatures + r:false, @WalkInPlace + duration:1 + r:false

#Dribble
@ChangeAction + action:going_to_ball, @CancelPathplanning, @LookAtBall, @LookAtFront, @DribbleForward

#DribbleWithAvoidance
$AvoidBall
    NO --> $BallClose + distance:%body.ball_reapproach_dist + angle:%body.ball_reapproach_angle
        YES --> $BallKickArea
            NEAR --> #Dribble
            FAR --> @ChangeAction + action:going_to_ball, @LookAtFront, @GoToBall + target:map_goal 
        NO --> @ChangeAction + action:going_to_ball + r:false, @LookAtFieldFeatures + r:false, @AvoidBallActive + r:false, @GoToBall + target:map_goal + blocking:false + distance:%body.ball_far_approach_dist
    YES --> $ReachedPathPlanningGoalPosition + thres:%body.ball_far_approach_position_thresh
        YES --> @AvoidBallInactive
        NO --> @ChangeAction + action:going_to_ball, @LookAtFieldFeatures, @GoToBall + target:map_goal + distance:%body.ball_far_approach_dist

#GoAndDribbleMapGoal
$BallKickArea
    NEAR --> $AlignedToGoal
        YES --> #Dribble
        NO --> #DribbleWithAvoidance
    ELSE --> #DribbleWithAvoidance

#GoAndDribbleDetectionGoal
$AlignedToPathPlanningGoal
    YES --> $BallKickArea
        NEAR --> #Dribble
        FAR --> @ChangeAction + action:going_to_ball, @GoToBall + target:detection_goal
    NO --> @ChangeAction + action:going_to_ball, @GoToBall + target:detection_goal

#GoAndDribbleAway
$BallKickArea
    NEAR --> #Dribble
    FAR --> @ChangeAction + action:going_to_ball, @GoToBall + target:close

#NoLocalizationPlayerBehavior
$BallSeen
    YES --> $BallClose
        YES --> $GoalSeen
            YES --> #GoAndDribbleDetectionGoal
            NO --> #GoAndDribbleAway
        NO --> @ChangeAction + action:going_to_ball, @GoToBall + target:close
    NO --> #SearchBall

#NoLocalizationGoalieBehavior
$BallSeen
    YES --> $BallClose
        YES --> #GoAndDribbleAway
        NO --> @ChangeAction + action:waiting, @LookAtBall, @Stand
    NO --> #SearchBall

#PositioningReady
$ConfigRole
    GOALIE --> $GoalScoreRecently
        YES --> @Stand + duration:0.1 + r:false, @PlayAnimationCheering + r:false, @GetWalkready + r:false
        NO --> $LocalizationAvailable
            YES --> @ChangeAction + action:positioning, @LookAtFieldFeatures, @Stand + duration:%body.ready_wait_time, @AvoidBallActive, @GoToRolePosition
            NO --> @ChangeAction + action:positioning, @AvoidBallActive, @GoToRelativePosition + x:2 + y:0 + t:0, @Stand
    ELSE --> $LocalizationAvailable
        YES --> @ChangeAction + action:positioning, @LookAtFieldFeatures, @Stand + duration:%body.ready_wait_time, @AvoidBallActive, @GoToRolePosition
        NO --> @ChangeAction + action:positioning, @AvoidBallActive, @GoToRelativePosition + x:2 + y:0 + t:0, @Stand

#ReorientationPause
@ChangeAction + action:localizing + r:false, @LookAtFieldFeatures + r:false, @Stand + duration:1.5 + r:false

#GoalieBehavior
$ClosestToBall + use_time_to_ball:true
    NO --> @ChangeAction + action:positioning, @AvoidBallActive, @LookAtFieldFeatures, @GoToBlockPosition
    YES --> $BallKickArea // TODO: refactor dribble or kick subtree
        NEAR --> $AlignedToGoal
            YES --> #PerformKick
            NO --> #DribbleWithAvoidance
        ELSE --> #DribbleWithAvoidance

#StrikerRole
$GoalieHandlingBall
    YES --> @ChangeAction + action:positioning, @AvoidBallActive, @LookAtFieldFeatures, @GoToPassAcceptPosition //don't go too far back
    NO --> #GoAndDribbleMapGoal

#DefensePositioning
@LookAtFieldFeatures, @ChangeAction + action:positioning, @GoToDefensePosition

#DefenderRole
$BallInOwnPercent + p:40
    YES --> $ClosestToBall
        YES --> #GoAndDribbleMapGoal
        NO --> #DefensePositioning
    NO --> #DefensePositioning


#SupporterRole
$PassStarted
    YES --> $BallSeen
        YES --> @LookAtBall, @ChangeAction + action:positioning, @AvoidBallActive, @GoToPassAcceptPosition
        NO --> @SearchBall, @ChangeAction + action:positioning, @AvoidBallActive, @GoToPassAcceptPosition
    NO --> @LookAtFieldFeatures, @ChangeAction + action:positioning, @AvoidBallActive, @GoToPassPreparePosition

#GoalieRole
$LocalizationAvailable
    YES --> $LocalizationPrecision
        HIGH --> $CurrentScore
            BEHIND --> $LastPlayer
                YES --> #StrikerRole
                NO --> #GoalieBehavior
            ELSE --> #GoalieBehavior
        LOW --> #ReorientationPause
    NO --> #ReorientationPause

#PenaltyShootoutBehavior
$SecondaryStateTeamDecider
    OUR --> @StandAndWaitRandom + min:10 + max:30, @KickBallDynamic + r:true + type:penalty, @Stand
    OTHER --> $BallDangerous + radius:1.3
        LEFT --> @PlayAnimationGoalieFallLeft, @Stand
        RIGHT --> @PlayAnimationGoalieFallRight, @Stand
        CENTER --> @PlayAnimationGoalieFallCenter, @Stand
        ELSE --> $BallSeen
            YES --> @LookAtBall, @Stand
            NO --> @SearchBall, @Stand


#Placing
$DoOnce
    NOT_DONE --> @ForgetBall, @LookAtFieldFeatures
    DONE --> $ConfigRole
        GOALIE --> @ChangeAction + action:positioning, @GoToBlockPosition, @LookAtFieldFeatures
        ELSE --> $BallSeen
            NO --> $SecondaryStateTeamDecider
                OUR --> #SearchBall
                OTHER --> @AvoidBallActive, @LookAtFieldFeatures, @WalkInPlace + duration:2, @GoToRelativePosition + x:1 + y:0 + t:0, @Stand
            YES --> $SecondaryStateDecider
                CORNER_KICK --> $SecondaryStateTeamDecider
                    OUR --> $RankToBallNoGoalie
                        FIRST --> @ChangeAction + action:positioning, @LookAtFieldFeatures, @AvoidBallActive, @GoToCornerKickPosition + mode:striker
                        SECOND --> @ChangeAction + action:positioning, @LookAtFieldFeatures, @AvoidBallActive, @GoToCornerKickPosition + mode:supporter
                        THIRD --> @ChangeAction + action:positioning, @LookAtFieldFeatures, @AvoidBallActive, @GoToDefensePosition
                    OTHER --> @ChangeAction + action:positioning, @LookAtFieldFeatures, @AvoidBallActive, @GoToCornerKickPosition + mode:others
                ELSE --> $SecondaryStateTeamDecider
                    OUR --> $RankToBallNoGoalie
                        FIRST --> @ChangeAction + action:positioning, @LookAtFieldFeatures, @AvoidBallActive, @GoToBall + target:gradient_goal + distance:0.5
                        SECOND --> @ChangeAction + action:positioning, @LookAtFieldFeatures, @AvoidBallActive, @GoToPassPreparePosition
                        THIRD --> @ChangeAction + action:positioning, @LookAtFieldFeatures, @AvoidBallActive, @GoToDefensePosition
                    OTHER --> $RankToBallNoGoalie
                        FIRST --> @ChangeAction + action:positioning, @LookAtFieldFeatures, @AvoidBallActive, @GoToDefensePosition + mode:freekick_first
                        SECOND --> @ChangeAction + action:positioning, @LookAtFieldFeatures, @AvoidBallActive, @GoToDefensePosition + mode:freekick_second
                        THIRD --> @ChangeAction + action:positioning, @LookAtFieldFeatures, @AvoidBallActive, @GoToDefensePosition

#Init
@Stand + duration:0.1 + r:false, @ChangeAction + action:waiting, @LookForward, @Stand

#NormalBehavior
$BallSeen
    NO --> $ConfigRole
        GOALIE --> @LookAtFieldFeatures, @GoToRolePosition
        ELSE --> #SearchBall
    YES --> $KickOffTimeUp
        NO_NORMAL --> #StandAndLook
        NO_FREEKICK --> #Placing
        YES --> $ConfigRole
            GOALIE --> #GoalieRole
            ELSE --> $LocalizationAvailable
                YES --> $LocalizationPrecision
                    HIGH --> $RankToBallNoGoalie
                        FIRST --> #StrikerRole
                        SECOND --> #SupporterRole
                        THIRD --> #DefensePositioning
                    LOW --> #ReorientationPause
                NO --> #ReorientationPause
-->BodyBehavior
$IsPenalized
    YES --> #DoNothing
    JUST_UNPENALIZED --> $GameStateDecider
        INITIAL --> #Init
        ELSE --> #GetWalkreadyAndLocalize
    NO --> $GameStateDecider
        INITIAL --> #Init
        READY --> $AnyGoalScoreRecently + time:50
            YES --> #PositioningReady
            NO --> $DoOnce
                NOT_DONE --> @ChangeAction + action:waiting + r:false, @LookAtFieldFeatures + r:false, @Stand + duration:1, @GetWalkready + r:false
                DONE --> #PositioningReady
        SET --> $SecondaryStateDecider
            PENALTYSHOOT --> $SecondaryStateTeamDecider
                OUR --> @Stand + duration:0.1 + r:false, @DeactivateHCM + r:false, @LookForward + r:false, @PlayAnimationInitInSim + r:false, @GetWalkready + r:false, @Stand // we need to also see the goalie
                OTHER --> $BallSeen 
                    YES --> @Stand + duration:0.1 + r:false, @DeactivateHCM + r:false, @LookForward + r:false, @PlayAnimationInitInSim + r:false, @GetWalkready + r:false, @LookAtBall + r:false, @PlayAnimationGoalieArms + r:false, @Stand // goalie only needs to care about the ball
                    NO --> @Stand + duration:0.1 + r:false, @DeactivateHCM + r:false, @LookForward + r:false, @PlayAnimationInitInSim + r:false, @GetWalkready + r:false, @SearchBall + r:false, @PlayAnimationGoalieArms + r:false, @Stand
            ELSE --> #StandAndLook
        FINISHED --> #Init
        PLAYING --> $SecondaryStateDecider
            PENALTYSHOOT --> #PenaltyShootoutBehavior
            TIMEOUT --> #StandAndLook
            ELSE --> $SecondaryStateModeDecider
                ELSE --> #StandAndLook
                PLACING --> #Placing
            NORMAL --> #NormalBehavior
            OVERTIME --> #NormalBehavior
