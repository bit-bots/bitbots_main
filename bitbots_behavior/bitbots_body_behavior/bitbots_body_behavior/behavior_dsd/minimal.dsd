#DoNothing
@LookForward, @Stand

#Init
@Stand + duration:0.1 + r:false, @LookForward, @Stand

#GetWalkreadyAndLocalize
@PlayAnimationInitInSim + r:false, @LookAtFieldFeatures + r:false, @GetWalkready + r:false, @WalkInPlace + duration:3 + r:false

#WalkInGoalie
@GoToRelativePosition + x:3 + y:2 + t:90 + threshold:0.4, @WalkInPlace + duration:0.1, @LookForward, @Stand

#WalkInPlayer
@GoToRelativePosition + x:3 + y:0 + t:90 + threshold:0.4, @WalkInPlace + duration:0.1, @LookForward, @Stand

#Dribble
@CancelPathplanning, @LookAtBall, @LookAtFront, @DribbleForward

-->BodyBehavior
$IsPenalized
    YES --> #DoNothing
    JUST_UNPENALIZED --> $GameStateDecider
        INITIAL --> #Init
        ELSE --> #GetWalkreadyAndLocalize
    NO --> $GameStateDecider
        INITIAL --> #Init
        READY --> $AnyGoalScoreRecently + time:50
            YES --> $ConfigRole
                GOALIE --> @Stand
                ELSE --> $DoOnce
                    NOT_DONE --> #GetWalkreadyAndLocalize
                    DONE --> #WalkInPlayer
            NO --> $ConfigRole
                GOALIE --> $DoOnce
                    NOT_DONE --> #GetWalkreadyAndLocalize
                    DONE --> #WalkInGoalie
                ELSE --> $DoOnce
                    NOT_DONE --> #GetWalkreadyAndLocalize
                    DONE --> #WalkInPlayer
        SET --> #DoNothing
        PLAYING --> $ConfigRole
            GOALIE --> @WalkInPlace + duration:0.1, @Stand
            ELSE --> $BallSeen
                YES --> #Dribble
                NO --> @SearchBall, @GoToRelativePosition + x:-2 + y:0 + t:0 + threshold:0.3
        ELSE --> #Init
